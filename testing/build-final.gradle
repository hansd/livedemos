import groovy.text.SimpleTemplateEngine
import groovy.text.Template
import groovy.text.TemplateEngine

apply plugin: 'groovy'

version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    groovy 'org.codehaus.groovy:groovy-all:1.7.1@jar'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testCompile group: 'junit', name: 'junit', version: '4.7', files('libs/spock-core-0.4-groovy-1.7-SNAPSHOT.jar')
}

clean {
    delete 'src/test'
}

task createTests(type: CreateTests) {
    template = file("src/templates/test.groovy")
    packages = 11
    tests = 20
}

test {
    forkEvery = 50
    maxParallelForks = 2
    def longRunners = []
    afterTest { descr, result ->
        def time = result.endTime - result.startTime
        if (time > 100) {
            logger.warn("Test: $descr.className is long running.")
            longRunners << descr.className
        }
    }
    afterSuite { descr, result ->
        if (!descr.parent) {
            file("$buildDir/longrunners").text = longRunners.join('\n')
        }
    }
}

class CreateTests extends DefaultTask {
    @InputFile
    File template

    @Input
    int packages

    @Input
    int tests

    @OutputDirectory
    File out = new File("src/test/groovy")

    @TaskAction
    def create() {
        project.delete out
        def engine = new SimpleTemplateEngine()
        packages.times { packageCounter ->
            String packageName = "org.gradle.tests$packageCounter"
            def path = project.mkdir("" + out + "/" + packageName.replace(".", "/"))
            tests.times { testCounter ->
                def testName = "Test$testCounter"
                def binding = [
                        packageName: packageName,
                        testName: testName,
                        sleep: 5
                ]
                project.file("$path/${testName}.groovy").text = engine.createTemplate(template).make(binding).toString()
            }
        }
    }
}
